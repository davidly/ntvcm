I don't know if you have the full version of MyZ80 which Lee Bradley put
together. It comes in a ZIP file called maxz80.zip.

http://primepuzzle.com/mouse/maxz80.zip

This is the same emulator as the demo version (written by Australian
Simeon Cran who I believe now works as a Software Engineer at Microsoft
after graduating and who wrote MyZ80 while a student, but I don't believe
he maintains it anymore.) The demo version is quite crippled and not
compatible diskwise with the full version so interested parties are
probably better-off not to install the demo version at all and should
just download Lee's full version. (One thing that is notably missing in
the demo version is the SUBMIT command and in fact the demo version is so
crippled that it is somewhat discouraging for anyone who doesn't know or
remember much about CP/M 80 to do anything.)

Lee has added many programs to maxz80 and preconfigured all of this so
the environment is large but excellent. To start, unzip maxz80 into a
single directory (call it maxz80), open a DOS window, and type MyZ80.

A Z-System Tutorial Through Examples

http://primepuzzle.com/mouse/hints0.htm

MaxZ80 comes with a large empty C: drive disk image. Don't use it for
creating a collection of programs. It is too large to even zip-up and
send to a friend with programs on it, or to put on line as a
preconfigured demo disk or anything like that. Fortunately MaxZ80
provides a mechanism for assigning the C: drive on start-up using an
environment variable called C.DSK. Open a DOS prompt in the maxz80
directory. Type the following at the prompt where ANYNAME.DSK is the name
of the disk image that you want to use:

SET C.DSK=ANYNAME.DSK

If the disk named does not exist it will be created with the smallest
size possible. As programs and files are added (imported or written
directly within the emulator) the disk image will grow as required.
However the disk image size will not shrink when programs and files are
deleted so when putting a demo disk or a collection disk together the
best thing to do is to put all your files into the maxz80 directory. Then
call myz80 with a non existent disk image named as the C: drive. When the
files for the intended collection are imported (by you) they will be
added to the new diskimage in as space efficient a manner as possible and
the diskimage will be as small as it can be. Clean-up your files in the
maxz80 directory afterwards.

The following example shows how this can work by using a file list with
myz80's submit command to create a new disk image filled with files. This
example is fairly long and can be skipped if you just want to try-out
what I have said on your own.

x--- snip - makemy.bat ---x

@echo off
if not exist c:\maxz80\*.* goto END
if "%CROOT%" == "" goto END
if "%1" == "Z80" goto BEGIN
cls
REM MAKEMY.BAT (C) Copyright Bill Buckels 2008
REM this batchfile assumes that maxz80 is installed in c:\maxz80
REM it further assumes that it is being run from within the
REM C:\AZ8106d\MyZ80 subdirectory from within the
REM Aztec C Z80 Build Environment under XP

REM create the main CPM submit file

echo ERA MAKEMY.SUB>MYMAIN.SUB
echo IMPORT MAKEMY.SUB>>MYMAIN.SUB
echo SUBMIT MAKEMY.SUB>>MYMAIN.SUB

REM finish the secondary CPM submit file
REM add the rest of the files
REM call this batch recursiveley to do so.

for %%f in (*.com) do call makemy.bat Z80 %%f
for %%f in (*.ovr) do call makemy.bat Z80 %%f

REM finish-off the submit file
REM so it automatically exits CPM when done
echo EXIT>>MAKEMY.SUB

REM we are done the prep-work now.
REM copy the baggage to the cpm emulator directory
copy *.com c:\maxz80\. >NUL
copy *.ovr c:\maxz80\. >NUL
copy MYMAIN.SUB c:\maxz80\. >NUL
copy MAKEMY.SUB c:\maxz80\. >NUL

REM start the emulator
REM this is the manual part.
REM the user needs to switch to drive C:
REM then IMPORT and SUBMIT MYMAIN.SUB
REM the rest is automatic.

SET C.DSK=AZSAMPLE.DSK
cd c:\maxz80\
echo This is the non-automated part of this process.
echo 1. After reading this, press a key. MYZ80 will start.
echo 2. You will then need to change to Drive C in MYZ80.
echo    Do this by typing C: then pressing the Enter Key.
echo 3. You will now need to build your disk.
echo    Do this by typing IMPORT MYMAIN.SUB them
echo    SUBMIT MYMAIN.SUB and press the Enter Key.
echo 4. The rest is automated beyond that point.
pause
cls
call MYZ80
SET C.DSK=


REM copy the finished disk back
copy AZSAMPLE.DSK C:\AZ80106d\MyZ80\. >NUL
cd C:\AZ80106d\MyZ80
cls
goto END

:BEGIN

REM this is the recursively called area

echo IMPORT %2>>MAKEMY.SUB

:END

